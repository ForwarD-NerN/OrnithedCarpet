plugins {
	id 'maven-publish'
	id 'fabric-loom' version '1.4-SNAPSHOT' apply false
	id 'ploceus' version '1.4-SNAPSHOT' apply false
	// https://github.com/ReplayMod/preprocessor
	// https://github.com/Fallen-Breath/preprocessor
	id 'com.replaymod.preprocess' version '20c7ec554a'
}

preprocess {
	def mc106 = createNode('1.6.4', 1_06_04, 'yarn')
	def mc107 = createNode('1.7.10', 1_07_10, 'yarn')
	def mc108 = createNode('1.8.9', 1_08_09, 'yarn')
	def mc109 = createNode('1.9.4', 1_09_04, 'yarn')
	def mc110 = createNode('1.10.2', 1_10_02, 'yarn')
	def mc111 = createNode('1.11.2', 1_11_02, 'yarn')
	def mc112 = createNode('1.12.2', 1_12_02, 'yarn')
	def mc113 = createNode('1.13.2', 1_13_02, 'yarn')  // root

	mc106.link(mc107, null)
	mc107.link(mc108, null)
	mc108.link(mc109, null)
	mc109.link(mc110, null)
	mc110.link(mc111, null)
	mc111.link(mc112, null)
	mc112.link(mc113, null)
}

tasks.register('buildAndGather') {
	subprojects {
		dependsOn project.tasks.named('build').get()
	}
	doFirst {
		println 'Gathering builds'
		def buildLibs = {
			p -> p.buildDir.toPath().resolve('libs')
		}
		delete fileTree(buildLibs(rootProject)) {
			include '*'
		}
		subprojects {
			copy {
				from(buildLibs(project)) {
					include '*.jar'
					exclude '*-dev.jar', '*-sources.jar'
				}
				into buildLibs(rootProject)
				duplicatesStrategy DuplicatesStrategy.INCLUDE
			}
		}
	}
}
